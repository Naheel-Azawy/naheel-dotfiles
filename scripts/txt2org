#!/usr/bin/gawk -f

# https://orgmode.org/worg/org-hacks.html
#
# Read tab separated data from STDIN and output an Org-mode table.
#
# Optional command line arguments specify row numbers at which to
# insert hlines.

BEGIN {
    for (i = 1; i < ARGC; i++) {
        hlines[ARGV[i] + 1] = 1;
        ARGV[i] = "-";
    }
}

{
    if (NF > max_nf) {
        max_nf = NF;
    }
    for (f = 1; f <= NF; f++) {
        if (length($f) > lengths[f]) {
            lengths[f] = length($f);
        }
        row[NR][f] = $f;
    }
}

END {
    hline_str = "|";
    for (f = 1; f <= max_nf; f++) {
        for (i = 0; i < (lengths[f] + 2); i++) {
            hline_str = hline_str "-";
        }
        if (f != max_nf) {
            hline_str = hline_str "+";
        } else {
            hline_str = hline_str "|";
        }
    }

    for (r = 1; r <= NR; r++) { # rows
        if (hlines[r] == 1) {
            print hline_str;
        }
        printf "|";
        for (f = 1; f <= max_nf; f++) { # columns
            cell = row[r][f];
            padding = "";
            for (i = 0; i < (lengths[f] - length(cell)); i++) {
                padding = padding " ";
            }
            # for now just print everything right-aligned
            # if(cell ~ /[0-9.]/){ printf " %s%s |", cell, padding; }
            # else{                printf " %s%s |", padding, cell; }
            printf " %s%s |", padding, cell;
        }
        printf "\n";
    }

    if (hlines[NR + 1]) {
        print hline_str;
    }
}
